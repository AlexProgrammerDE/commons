subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "signing"
    apply plugin: "checkstyle"

    java.toolchain.languageVersion = JavaLanguageVersion.of(8)
    compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

    archivesBaseName = "commons-" + project.maven_name
    group = project.maven_group
    version = project.maven_version

    repositories {
        mavenCentral()
        maven {
            name = "lenni0451 releases"
            url = "https://maven.lenni0451.net/releases"
        }
    }

    dependencies {
        compileOnly "com.google.code.findbugs:jsr305:3.0.2"
        compileOnly "org.projectlombok:lombok:1.18.30"
        annotationProcessor "org.projectlombok:lombok:1.18.30"

        testImplementation platform("org.junit:junit-bom:5.9.3")
        testImplementation "org.junit.jupiter:junit-jupiter"
        testImplementation "org.ow2.asm:asm:9.6"
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    javadoc {
        failOnError = false
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
        maxParallelForks Runtime.runtime.availableProcessors()
    }

    publishing {
        repositories {
            maven {
                name = "reposilite"
                def releasesUrl = "https://maven.lenni0451.net/releases"
                def snapshotsUrl = "https://maven.lenni0451.net/snapshots"
                url = project.maven_version.endsWith("SNAPSHOT") ? snapshotsUrl : releasesUrl

                credentials(PasswordCredentials)
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
        publications {
            maven(MavenPublication) {
                artifactId = project.maven_name
                groupId = project.maven_group
                version = project.maven_version

                from components.java

                pom {
                    name = rootProject.name
                    description = "A java library with many useful functions and classes"
                    url = "https://github.com/Lenni0451/commons"
                    licenses {
                        license {
                            name = "MIT License"
                            url = "https://github.com/Lenni0451/commons/blob/main/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = "Lenni0451"
                        }
                    }
                    scm {
                        connection = "scm:git:git://github.com/Lenni0451/commons.git"
                        developerConnection = "scm:git:ssh://github.com/Lenni0451/commons.git"
                        url = "github.com/Lenni0451/commons"
                    }
                }
            }
        }
    }

    checkstyle {
        toolVersion = "9.3" //Latest version for Java 8
        configFile = rootProject.file("config/checkstyle/checkstyle.xml")
    }

    signing {
        sign configurations.archives
        sign publishing.publications.maven
    }

    project.tasks.withType(PublishToMavenRepository).forEach {
        it.dependsOn(project.tasks.withType(Sign))
    }
    build.dependsOn(test)
    build.dependsOn(check)
}
